package cep;

import com.ftn.sbnz.model.models.GlavnaParcela
import com.ftn.sbnz.model.models.BiljnaKultura
import com.ftn.sbnz.model.models.JacinaVetra
import com.ftn.sbnz.model.models.PreporukaBiljka
import com.ftn.sbnz.model.models.GrupaZrenja
import com.ftn.sbnz.model.models.MeteoroloskiPodaci
import com.ftn.sbnz.model.models.PreporukaGrupaZrenja
import com.ftn.sbnz.model.models.PosadjenaKultura
import java.time.LocalDateTime;
import java.time.Month;

rule "Preporuka SUNCOKRET"
    when
        $gp: GlavnaParcela(
            humus > 0.005,
            ocekivanaJacinaVetra == JacinaVetra.SLAB)
        Number(intValue == 0) from accumulate(
            PosadjenaKultura(
                parcelaId == $gp.getId(),
                kultura == BiljnaKultura.SUNCOKRET
            ) over window:time(365d), count(1))
        not(PreporukaBiljka(kultura == BiljnaKultura.SUNCOKRET,
            glavnaParcela.getId() == $gp.getId()))
    then
        System.out.println("======== Preporuka SUNCOKRET =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.SUNCOKRET, $gp));
        $gp.getPreporuke().add("SUNCOKRET");
end

rule "Preporuka SOJA"
    when
        $gp: GlavnaParcela(
            humus > 0.01,
            ocekivanaJacinaVetra != JacinaVetra.JAK)
        Number(intValue == 0) from accumulate(
            PosadjenaKultura(
                parcelaId == $gp.getId(),
                kultura == BiljnaKultura.SOJA
            ) over window:time(365d), count(1))    
        not(PreporukaBiljka(kultura == BiljnaKultura.SOJA,
            glavnaParcela.getId() == $gp.getId()))
    then
        System.out.println("======== Preporuka SOJA =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.SOJA, $gp));
        $gp.getPreporuke().add("SOJA");
end

rule "Preporuka PSENICA"
    when
        $gp: GlavnaParcela(
            humus > 0.01,
            ocekivanaJacinaVetra != JacinaVetra.JAK)
        Number(intValue == 0) from accumulate(
            PosadjenaKultura(
                parcelaId == $gp.getId(),
                kultura == BiljnaKultura.PSENICA
            ) over window:time(365d), count(1))            
        not(PreporukaBiljka(kultura == BiljnaKultura.PSENICA,
            glavnaParcela.getId() == $gp.getId()))
    then
        System.out.println("======== Preporuka psenica =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.PSENICA, $gp));
        $gp.getPreporuke().add("PSENICA");
end

rule "Preporuka ULJANA_REPICA"
    when
        $gp: GlavnaParcela(
            humus > 0.01,
            ocekivanaJacinaVetra != JacinaVetra.JAK
            )
        Number(intValue == 0) from accumulate(
            PosadjenaKultura(
                parcelaId == $gp.getId(),
                kultura == BiljnaKultura.ULJANA_REPICA
            ) over window:time(365d), count(1))
        not(PreporukaBiljka(kultura == BiljnaKultura.ULJANA_REPICA,
            glavnaParcela.getId() == $gp.getId()))
    then
        System.out.println("======== Preporuka ULJANA_REPICA =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.ULJANA_REPICA, $gp));
        $gp.getPreporuke().add("ULJANA_REPICA");
end

rule "Preporuka SECERNA_REPA"
    when
        $gp: GlavnaParcela(humus > 0.03)
        Number(intValue == 0) from accumulate(
            PosadjenaKultura(
                parcelaId == $gp.getId(),
                kultura == BiljnaKultura.SECERNA_REPA
            ) over window:time(365d), count(1))            
        not(PreporukaBiljka(kultura == BiljnaKultura.SECERNA_REPA,
            glavnaParcela.getId() == $gp.getId()))
    then
        System.out.println("======== Preporuka SECERNA_REPA =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.SECERNA_REPA, $gp));
        $gp.getPreporuke().add("SECERNA_REPA");
end

rule "Preporuka KUKURUZ"
    when
        $gp: GlavnaParcela( 
            humus > 0.03,
            ocekivanaJacinaVetra == JacinaVetra.SLAB)
        Number(intValue == 0) from accumulate(
            PosadjenaKultura(
                parcelaId == $gp.getId(),
                kultura == BiljnaKultura.KUKURUZ
            ) over window:time(365d), count(1))            
        not(PreporukaBiljka(kultura == BiljnaKultura.KUKURUZ,
            glavnaParcela.getId() == $gp.getId()))
    then
        System.out.println("======== Preporuka KUKURUZ =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.KUKURUZ, $gp));
        $gp.getPreporuke().add("KUKURUZ");
end

rule "Rotacija PSENICA"
    when
        $gp: GlavnaParcela()
        not(PreporukaBiljka(kultura == BiljnaKultura.PSENICA,
            glavnaParcela.getId() == $gp.getId()))
        PosadjenaKultura(
            $timestamp: timestamp,
            kultura == BiljnaKultura.SUNCOKRET,
            parcelaId == $gp.getId()
        )
        not(
            PosadjenaKultura(
                timestamp > $timestamp,
                parcelaId == $gp.getId()
            )
        )
    then
        System.out.println("======== Preporuka PSENICA =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.PSENICA, $gp));
        $gp.getPreporuke().add("PSENICA");
end

rule "Rotacija ULJANA_REPICA"
    when
        $gp: GlavnaParcela()
        not(PreporukaBiljka(kultura == BiljnaKultura.ULJANA_REPICA,
            glavnaParcela.getId() == $gp.getId()))
        PosadjenaKultura(
            $timestamp: timestamp,
            kultura == BiljnaKultura.SOJA || kultura == BiljnaKultura.PSENICA,
            parcelaId == $gp.getId()
        )
        not(
            PosadjenaKultura(
                timestamp > $timestamp,
                parcelaId == $gp.getId()
            )
        )
    then
        System.out.println("======== Preporuka ULJANA_REPICA =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.ULJANA_REPICA, $gp));
        $gp.getPreporuke().add("ULJANA_REPICA");
end

rule "Rotacija KUKURUZ"
    when
        $gp: GlavnaParcela()
        not(PreporukaBiljka(kultura == BiljnaKultura.KUKURUZ,
            glavnaParcela.getId() == $gp.getId()))
        PosadjenaKultura(
            $timestamp: timestamp,
            kultura == BiljnaKultura.ULJANA_REPICA || kultura == BiljnaKultura.SECERNA_REPA,
            parcelaId == $gp.getId()
        )
        not(
            PosadjenaKultura(
                timestamp > $timestamp,
                parcelaId == $gp.getId()
            )
        )            
    then
        System.out.println("======== Preporuka KUKURUZ =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.KUKURUZ, $gp));
        $gp.getPreporuke().add("KUKURUZ");
end

rule "Rotacija SOJA"
    when
        $gp: GlavnaParcela()
        not(PreporukaBiljka(kultura == BiljnaKultura.SOJA,
            glavnaParcela.getId() == $gp.getId()))
        PosadjenaKultura(
            $timestamp: timestamp,
            kultura == BiljnaKultura.KUKURUZ,
            parcelaId == $gp.getId()
        )
        not(
            PosadjenaKultura(
                timestamp > $timestamp,
                parcelaId == $gp.getId()
            )
        )            
    then
        System.out.println("======== Preporuka SOJA =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.SOJA, $gp));
        $gp.getPreporuke().add("SOJA");
end

rule "Rotacija SECERNA_REPA"
    when
        $gp: GlavnaParcela()
        not(PreporukaBiljka(kultura == BiljnaKultura.SECERNA_REPA,
            glavnaParcela.getId() == $gp.getId()))
        PosadjenaKultura(
            $timestamp: timestamp,
            kultura == BiljnaKultura.SOJA || kultura == BiljnaKultura.PSENICA,
            parcelaId == $gp.getId()
        )
        not(
            PosadjenaKultura(
                timestamp > $timestamp,
                parcelaId == $gp.getId()
            )
        )            
    then
        System.out.println("======== Preporuka SECERNA_REPA =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.SECERNA_REPA, $gp));
        $gp.getPreporuke().add("SECERNA_REPA");
end

rule "Rotacija SUNCOKRET"
    when
        $gp: GlavnaParcela()
        not(PreporukaBiljka(kultura == BiljnaKultura.SUNCOKRET,
            glavnaParcela.getId() == $gp.getId()))
        PosadjenaKultura(
            $timestamp: timestamp,
            kultura == BiljnaKultura.SECERNA_REPA || kultura == BiljnaKultura.ULJANA_REPICA,
            parcelaId == $gp.getId()
        )
        not(
            PosadjenaKultura(
                timestamp > $timestamp,
                parcelaId == $gp.getId()
            )
        )            
    then
        System.out.println("======== Preporuka SUNCOKRET =======" + $gp.getId());
        insert(new PreporukaBiljka(BiljnaKultura.SUNCOKRET, $gp));
        $gp.getPreporuke().add("SUNCOKRET");
end

rule "Nova posadjena biljna kultura"
    lock-on-active true
    salience 2
    when
        PosadjenaKultura(
            $parcelaId: parcelaId
        )
        $gp: GlavnaParcela(id == $parcelaId)
        $pb: PreporukaBiljka(glavnaParcela.getId() == $parcelaId)
        $pgz: PreporukaGrupaZrenja(glavnaParcela.getId() == $parcelaId)
    then
        System.out.println("======== Nova PosadjenaKultura =======" + $gp.getId());
        delete($pb);
        delete($pgz);
        $gp.obrisiPreporuke();
end